plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.1.0"
}

group = 'net.toiletmc'
version = '1.4.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "placeholderapi"
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
    maven {
        name = "jitpack"
        url = uri("https://jitpack.io")
    }
//    maven {
//        name = "elmakers"
//        url = uri("https://maven.elmakers.com/repository/")
//    }
    maven { url("https://repo.lucko.me/") }
}

dependencies {
    // 不再需要 helper，移至独立插件。
    // compileOnly "me.lucko:helper:5.6.14"

    // https://github.com/ToiletMC/plugin-ToiletPAPI
    // PlaceholderAPI 已移至独立插件 ToiletPAPI，方便热加载而无需重启服务器。
    // compileOnly 'me.clip:placeholderapi:2.11.6'

    // 魔法插件的api中可能缺少某些功能，所以用Jar代替
    // compileOnly 'com.elmakers.mine.bukkit:MagicAPI:9.4-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.40'
    annotationProcessor 'org.projectlombok:lombok:1.18.40'
    compileOnly 'io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT'
    compileOnly "fr.xephi:authme:5.6.0-SNAPSHOT"
    compileOnly 'me.lucko:spark-api:0.1-SNAPSHOT'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.github.Zrips:CMI-API:9.7.14.3'
    compileOnly(fileTree(dir: "libs", includes: ["*.jar"]))

    // 暂时先不用这个
    // implementation "de.exlll:configlib-yaml:4.5.0"

    implementation "com.squareup.okhttp3:okhttp:5.1.0"
    implementation("de.tr7zw:item-nbt-api:2.15.2")
}

shadowJar {
    minimize()

    // 对 OkHttp 及其依赖进行 relocate 避免与其他插件冲突
    relocate 'okhttp3', 'net.toiletmc.toiletcore.libs.okhttp3'
    relocate 'okio', 'net.toiletmc.toiletcore.libs.okio'
    relocate 'kotlin', 'net.toiletmc.toiletcore.libs.kotlin'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.toiletmc.toiletcore.libs.nbtapi'
    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }
}


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
